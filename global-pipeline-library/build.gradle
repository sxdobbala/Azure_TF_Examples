defaultTasks 'clean', 'compileTestGroovy', 'deleteVarsClasses'

buildscript {
  repositories {
    maven {
      url "${artifactory_contextUrl}/repo"
      credentials {
        username = "${artifactory_user}"
        password = "${artifactory_password}"
      }
    }
  }
  dependencies {
    //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
    classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4+"
  }
}

apply plugin: "com.jfrog.artifactory"
apply plugin: "groovy"

artifactory {
  contextUrl = "${artifactory_contextUrl}"   //The base Artifactory URL if not overridden by the publisher/resolver
  resolve {
    repository {
      repoKey = 'repo'
      maven = true
    }
  }
}

dependencies {
  compileGroovy {
    groovyOptions.configurationScript = file("gradle/config.groovy")
  }

  compile(
    'com.cloudbees:groovy-cps:1.19',
    'org.jenkins-ci.plugins.workflow:workflow-step-api:2.16@jar',
    'org.apache.maven:maven-model:3.3.9',
    'org.codehaus.groovy:groovy-all:2.4.5',
    'org.jenkins-ci.main:jenkins-core:2.73.3',
    'org.jenkins-ci.plugins:credentials-binding:1.16@jar',
    'javax.servlet:javax.servlet-api:4.0.0',
    'com.fasterxml.jackson.core:jackson-databind:2.9.5',
    'org.kohsuke:github-api:1.92@jar',
    'com.google.code.gson:gson:2.8.2', // Used by ContrastEvent.groovy
    'com.contrastsecurity:contrast-sdk-java:2.7',
    'org.apache.ivy:ivy:2.5.0-rc1', // Used to @Grab
    'com.rallydev.rest:rally-rest-api:2.2.1', // used by automated compliance
    'org.apache.poi:poi:3.9', // used by automated compliance
    'com.optum.psas:securitymetrics-client:0.0.2', // used by automated compliance
  )

  testCompile(
    'org.yaml:snakeyaml:1.19',  //read test optumfile yaml
    'com.lesfurets:jenkins-pipeline-unit:1.1',
    'junit:junit:4.11',
    'commons-io:commons-io:2.6',
    'org.spockframework:spock-core:1.1-groovy-2.4',
    'cglib:cglib-nodep:3.2.5',  // Use with cglib to mock classes without default constructor
    'org.objenesis:objenesis:2.6' // allows mocking of classes without default constructor (together with CGLIB)
  )
}

repositories {
  maven {
    url "https://repo1.uhc.com/artifactory"
  }
}

sourceSets {
  main {
    groovy {
      srcDirs = ['src', 'vars']
    }
    resources {
      srcDirs = ['resources']
    }
  }
  test{
    groovy.srcDirs = [file('test/groovy')]
    resources.srcDirs = [file('test/resources')]
  }
}

test {
  dependsOn 'cleanTest'
  testLogging {
    events "PASSED", "FAILED", "SKIPPED"
  }
}

// After the build, delete the "vars" classes which are output to build/classes/main.
task deleteVarsClasses(type: Delete) {
    delete fileTree(dir:"$buildDir/classes/main", include: '*.class')
}
